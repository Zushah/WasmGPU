/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 *
 * Blocked real LU panel factorization for columns [kk, kk + pw).
 * The kernel factors the active panel, applies full-row swaps, and writes
 * pivot rows to `ipiv`.
 */

const WG_SIZE: u32 = 128u;

struct LuBlockedParams {
    batch_count: u32,
    n: u32,
    elems_per_matrix: u32,
    kk: u32,
    pw: u32,
    _pad0: u32,
    _pad1: u32,
    _pad2: u32,
}

@group(0) @binding(0) var<uniform> params: LuBlockedParams;
@group(0) @binding(1) var<storage, read_write> matrices: array<f32>;
@group(0) @binding(2) var<storage, read_write> ipiv: array<u32>;

var<workgroup> wg_abs: array<f32, WG_SIZE>;
var<workgroup> wg_row: array<u32, WG_SIZE>;
var<workgroup> pivot_row: u32;

@compute @workgroup_size(WG_SIZE, 1, 1)
fn main(@builtin(workgroup_id) wg_id: vec3<u32>, @builtin(local_invocation_index) lid: u32) {
    let b = wg_id.x;
    if (b >= params.batch_count) {
        return;
    }
    let n = params.n;
    let base = b * params.elems_per_matrix;
    let base_ipiv = b * n;
    let kk = params.kk;
    let pw = params.pw;
    for (var j: u32 = 0u; j < pw; j = j + 1u) {
        let col = kk + j;
        if (col >= n) {
            break;
        }
        var pv: f32 = -1.0;
        var pr: u32 = col;
        var ii = col + lid;
        while (ii < n) {
            let aik = matrices[base + ii * n + col];
            let av = abs(aik);
            if (av > pv || (av == pv && ii < pr)) {
                pv = av;
                pr = ii;
            }
            ii = ii + WG_SIZE;
        }
        wg_abs[lid] = pv;
        wg_row[lid] = pr;
        workgroupBarrier();
        var s: u32 = WG_SIZE >> 1u;
        while (s > 0u) {
            if (lid < s) {
                let i1 = lid + s;
                let av0 = wg_abs[lid];
                let av1 = wg_abs[i1];
                let r0 = wg_row[lid];
                let r1 = wg_row[i1];
                if (av1 > av0 || (av1 == av0 && r1 < r0)) {
                    wg_abs[lid] = av1;
                    wg_row[lid] = r1;
                }
            }
            workgroupBarrier();
            s = s >> 1u;
        }
        if (lid == 0u) {
            pivot_row = wg_row[0];
            ipiv[base_ipiv + col] = pivot_row;
        }
        workgroupBarrier();
        let piv = pivot_row;
        var jj: u32 = lid;
        while (jj < n) {
            let ia = base + col * n + jj;
            let ib = base + piv * n + jj;
            let va = matrices[ia];
            let vb = matrices[ib];
            matrices[ia] = vb;
            matrices[ib] = va;
            jj = jj + WG_SIZE;
        }
        workgroupBarrier();
        let pivval = matrices[base + col * n + col];
        var t: u32 = col + 1u + lid;
        while (t < n) {
            let idx = base + t * n + col;
            matrices[idx] = matrices[idx] / pivval;
            t = t + WG_SIZE;
        }
        workgroupBarrier();
        let endc = min(n, kk + pw);
        let inner_cols = endc - (col + 1u);
        if (inner_cols > 0u) {
            let inner_rows = n - (col + 1u);
            let total = inner_rows * inner_cols;
            var u: u32 = lid;
            while (u < total) {
                let row_t = u / inner_cols;
                let col_t = u % inner_cols;
                let i = col + 1u + row_t;
                let c = col + 1u + col_t;
                let lik = matrices[base + i * n + col];
                let ucj = matrices[base + col * n + c];
                let idx = base + i * n + c;
                matrices[idx] = matrices[idx] - lik * ucj;
                u = u + WG_SIZE;
            }
            workgroupBarrier();
        }
    }
}
