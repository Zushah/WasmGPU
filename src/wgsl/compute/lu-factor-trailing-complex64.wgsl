/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 *
 * Blocked complex64 LU trailing update.
 * The kernel updates A22 -= L21 * U12 for the active panel using
 * workgroup tiles.
 */

const TILE_M: u32 = 16u;
const TILE_N: u32 = 8u;

struct LuBlockedParams {
    batch_count: u32,
    n: u32,
    elems_per_matrix: u32,
    kk: u32,
    pw: u32,
    _pad0: u32,
    _pad1: u32,
    _pad2: u32,
}

@group(0) @binding(0) var<uniform> params: LuBlockedParams;
@group(0) @binding(1) var<storage, read_write> matrices: array<vec2<f32>>;

var<workgroup> l_tile: array<vec2<f32>, TILE_M>;
var<workgroup> u_tile: array<vec2<f32>, TILE_N>;

fn cx_mul(a: vec2<f32>, b: vec2<f32>) -> vec2<f32> {
    return vec2<f32>(a.x * b.x - a.y * b.y, a.x * b.y + a.y * b.x);
}

@compute @workgroup_size(TILE_M, TILE_N, 1)
fn main(
    @builtin(workgroup_id) wg_id: vec3<u32>,
    @builtin(local_invocation_id) lid: vec3<u32>,
    @builtin(local_invocation_index) lid_idx: u32,
) {
    let b = wg_id.z;
    if (b >= params.batch_count) {
        return;
    }
    let n  = params.n;
    let kk = params.kk;
    let pw = params.pw;
    let base = b * params.elems_per_matrix;
    let m_dim = n - (kk + pw);
    let n_dim = n - (kk + pw);
    if (m_dim == 0u || n_dim == 0u || pw == 0u) {
        return;
    }
    let global_i = wg_id.y * TILE_M + lid.x;
    let global_j = wg_id.x * TILE_N + lid.y;
    let valid = (global_i < m_dim) && (global_j < n_dim);
    var acc = vec2<f32>(0.0, 0.0);
    for (var k: u32 = 0u; k < pw; k = k + 1u) {
        if (lid_idx < TILE_M) {
            let i_g = wg_id.y * TILE_M + lid_idx;
            if (i_g < m_dim) {
                let row = (kk + pw) + i_g;
                l_tile[lid_idx] = matrices[base + row * n + (kk + k)];
            } else {
                l_tile[lid_idx] = vec2<f32>(0.0, 0.0);
            }
        } else if (lid_idx < TILE_M + TILE_N) {
            let j_local = lid_idx - TILE_M;
            let j_g = wg_id.x * TILE_N + j_local;
            if (j_g < n_dim) {
                let col = (kk + pw) + j_g;
                u_tile[j_local] = matrices[base + (kk + k) * n + col];
            } else {
                u_tile[j_local] = vec2<f32>(0.0, 0.0);
            }
        }
        workgroupBarrier();
        acc = acc + cx_mul(l_tile[lid.x], u_tile[lid.y]);
        workgroupBarrier();
    }
    if (valid) {
        let row = (kk + pw) + global_i;
        let col = (kk + pw) + global_j;
        let idx = base + row * n + col;
        matrices[idx] = matrices[idx] - acc;
    }
}
