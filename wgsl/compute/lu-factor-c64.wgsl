/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 *
 * Batched c64 LU factorization with partial pivoting.
 * Input matrices use row-major (batch, n, n) layout with one vec2<f32>
 * per entry. The kernel overwrites each matrix with compact L/U factors
 * and writes pivot rows to `ipiv`.
 */

struct LuBatchedParams {
    batch_count: u32,
    n: u32,
    elems_per_matrix: u32,
    _pad: u32,
}

@group(0) @binding(0) var<uniform> params: LuBatchedParams;
@group(0) @binding(1) var<storage, read_write> matrices: array<vec2<f32>>;
@group(0) @binding(2) var<storage, read_write> ipiv: array<u32>;

var<workgroup> wg_abs: array<f32, 128>;
var<workgroup> wg_row: array<u32, 128>;
var<workgroup> pivot_row: u32;

fn cx_mul(a: vec2<f32>, b: vec2<f32>) -> vec2<f32> {
    return vec2<f32>(a.x * b.x - a.y * b.y, a.x * b.y + a.y * b.x);
}

fn cx_div(a: vec2<f32>, b: vec2<f32>) -> vec2<f32> {
    let d = b.x * b.x + b.y * b.y;
    return vec2<f32>((a.x * b.x + a.y * b.y) / d, (a.y * b.x - a.x * b.y) / d);
}

fn cx_magsq(z: vec2<f32>) -> f32 {
    return z.x * z.x + z.y * z.y;
}

@compute @workgroup_size(128, 1, 1)
fn main(@builtin(workgroup_id) wg_id: vec3<u32>, @builtin(local_invocation_index) lid: u32) {
    let b = wg_id.x;
    if (b >= params.batch_count) {
        return;
    }
    let n = params.n;
    let stride = params.elems_per_matrix;
    let base = b * stride;
    let base_ipiv = b * n;
    for (var kk = 0u; kk < n; kk = kk + 1u) {
        var pv = -1.0;
        var pr = kk;
        var ii = kk + lid;
        while (ii < n) {
            let aik = matrices[base + ii * n + kk];
            let ms = cx_magsq(aik);
            if (ms > pv || (ms == pv && ii < pr)) {
                pv = ms;
                pr = ii;
            }
            ii = ii + 128u;
        }
        wg_abs[lid] = pv;
        wg_row[lid] = pr;
        workgroupBarrier();
        var s = 64u;
        while (s > 0u) {
            if (lid < s) {
                let i1 = lid + s;
                if (i1 < 128u) {
                    let av0 = wg_abs[lid];
                    let av1 = wg_abs[i1];
                    let r0 = wg_row[lid];
                    let r1 = wg_row[i1];
                    if (av1 > av0 || (av1 == av0 && r1 < r0)) {
                        wg_abs[lid] = av1;
                        wg_row[lid] = r1;
                    }
                }
            }
            workgroupBarrier();
            s = s >> 1u;
        }
        if (lid == 0u) {
            pivot_row = wg_row[0];
            ipiv[base_ipiv + kk] = pivot_row;
        }
        workgroupBarrier();
        let piv = pivot_row;
        var jj = lid;
        while (jj < n) {
            let ia = base + kk * n + jj;
            let ib = base + piv * n + jj;
            let va = matrices[ia];
            let vb = matrices[ib];
            matrices[ia] = vb;
            matrices[ib] = va;
            jj = jj + 128u;
        }
        workgroupBarrier();
        let p = matrices[base + kk * n + kk];
        let col_len = n - kk - 1u;
        var t = lid;
        while (t < col_len) {
            let i = kk + 1u + t;
            let ik = base + i * n + kk;
            matrices[ik] = cx_div(matrices[ik], p);
            t = t + 128u;
        }
        workgroupBarrier();
        let dim = n - kk - 1u;
        let total = dim * dim;
        t = lid;
        while (t < total) {
            let ii2 = t / dim;
            let jj2 = t % dim;
            let i = kk + 1u + ii2;
            let j = kk + 1u + jj2;
            let lik = matrices[base + i * n + kk];
            let ukj = matrices[base + kk * n + j];
            let ij = base + i * n + j;
            matrices[ij] = matrices[ij] - cx_mul(lik, ukj);
            t = t + 128u;
        }
        workgroupBarrier();
    }
}
